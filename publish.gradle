apply plugin: "maven-publish"
apply plugin: "signing"

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "MavenCentral"
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }
    publications.removeAll { it.name == "OSSRH" }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            suppressPomMetadataWarningsFor("testFixturesApiElements")
            suppressPomMetadataWarningsFor("testFixturesRuntimeElements")
            pom {
                name.set(project.name)
                description.set(project.name)
                url.set(project.ext.gitUrl)
                licenses {
                    license {
                        name.set("MIT License")
                        url.set("https://opensource.org/licenses/MIT")
                        distribution.set("repo")
                    }
                }
                developers {
                    developer {
                        id.set("mruoc")
                        name.set("Michael Ruocco")
                        email.set("michael.ruocco@hotmail.com")
                    }
                }
                scm {
                    url.set(project.ext.gitUrl)
                }
            }
        }
    }
}

signing {
    def signingKey = System.getenv("OSSRH_PGP_SECRET_KEY")
    def signingPassword = System.getenv("OSSRH_PGP_SECRET_KEY_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(":publishMavenJavaPublicationToMavenCentral")) {
        tasks.withType(Sign).configureEach {
            enabled = true
        }
    }
}