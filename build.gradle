plugins {
    id "com.diffplug.spotless" version "8.2.1"
    id "com.github.ben-manes.versions" version "0.53.0"
    id "nebula.lint" version "21.1.3"
    id "org.sonarqube" version "7.2.2.6593"
    id "pl.allegro.tech.build.axion-release" version "1.21.1"
    id "com.bmuschko.docker-remote-api" version "10.0.0"
    id "org.danilopianini.publish-on-central" version "9.1.12"
}

scmVersion {
    versionCreator "versionWithBranch"
    tag {
        prefix = "${name}-"
    }
}
project.version = scmVersion.version

subprojects {
    group "com.github.michaelruocco"

    tasks.withType(Javadoc).configureEach {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    ext {
        slf4jVersion = "2.0.17"
        lombokVersion = "1.18.42"
        postgresqlVersion = "42.7.10"
        flywayVersion = "11.19.0"
        jsonAdapterVersion = "1.0.5"
        springKafkaVersion = "3.1.0"
        springVersion = "6.2.2"
        jacksonVersion = "3.0.4"
        cognitoVersion = "2.30.1"
        commonsCollectionsVersion = "4.5.0"
        commonsIoVersion = "2.18.0"
        commonsText = "1.15.0"
        resilience4jVersion = "2.3.0"
        awaitilityVersion = "4.3.0"
        awsSdkVersion = "2.41.30"
        springBootVersion = "4.0.2"
        tesseractVersion = "5.18.0"

        junitVersion = "6.0.3"
    }

    configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == "org.springframework.boot") {
                details.useVersion "${springBootVersion}"
            }
        }
    }

    plugins.withId("java-library") { // Only apply to Java library modules
        apply plugin: "org.danilopianini.publish-on-central"

        java {
            withSourcesJar()
            withJavadocJar()
        }

        publishOnCentral {
            repoOwner = "michaelruocco"
            licenseName = "MIT License"
            licenseUrl = "https://opensource.org/licenses/MIT"
            projectUrl = "https://github.com/michaelruocco/crossword-solver-api"
            projectLongName.set("${rootProject.name}-${project.name}")
            projectDescription.set("${rootProject.name}-${project.name}")
        }

        publishing {
            publications {
                withType(MavenPublication).configureEach { pub ->
                    pub.suppressPomMetadataWarningsFor("testFixturesApiElements")
                    pub.suppressPomMetadataWarningsFor("testFixturesRuntimeElements")
                    pub.pom {
                        developers {
                            developer {
                                id.set("mruoc")
                                name.set("Michael Ruocco")
                                email.set("michael.ruocco@hotmail.com")
                            }
                        }
                    }
                }
            }
        }

        signing {
            def signingKey = System.getenv("MAVEN_CENTRAL_PGP_SECRET_KEY")
            def signingPassword = System.getenv("MAVEN_CENTRAL_PGP_SECRET_KEY_PASSWORD")
            useInMemoryPgpKeys(signingKey, signingPassword)
        }
    }
}

gradleLint {
    rules = ['all-dependency']
    alwaysRun = false
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "michaelruocco"
        property "sonar.projectKey", "michaelruocco_${name}"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.rootDir}/code-coverage-report/build/reports/jacoco/unitCoverage/unitCoverage.xml,${project.rootDir}/code-coverage-report/build/reports/jacoco/integrationCoverage/integrationCoverage.xml"
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}

docker {
    registryCredentials {
        username.set(System.getenv("DOCKER_USERNAME"))
        password.set(System.getenv("DOCKER_PASSWORD"))
    }
}

def dockerContext = layout.buildDirectory.dir("docker")
def bootJarFile = project(":spring-app").provider {
    project(":spring-app").tasks.named("bootJar").get().archiveFile.get().asFile
}
def prepareDockerContext = tasks.register("prepareDockerContext", Copy) {
    dependsOn(project(":spring-app").tasks.named("bootJar"))
    into(dockerContext)
    from(bootJarFile) {
        rename { "app.jar" }
    }
    from("adapters/solver-client/tessdata/eng.traineddata") {
        into("tessdata")
    }
    from("Dockerfile")
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

def buildImage = tasks.register("buildImage", DockerBuildImage) {
    dependsOn(prepareDockerContext)
    inputDir.set(dockerContext)
    def ciTag = "michaelruocco/${project.name}:${project.version}"
    def localTag = "local-${project.name}:latest"
    images.addAll([ciTag, localTag])
}

tasks.register("pushImage", DockerPushImage) {
    dependsOn(buildImage)
    images.set(buildImage.flatMap { it.images })
}